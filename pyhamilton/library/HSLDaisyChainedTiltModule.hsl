// ===========================================================================
// Interface of the daisy chained Tilt-Module library 
//
// Copyright (C) by HAMILTON Bonaduz AG, CH-7402 Bonaduz.
// All rights reserved.
// ===========================================================================
//
// Modification History
//		2008-07-08, V1.0: Ruedi Vogel, created, based on V1.4 Tilt-Module
// 
// ===========================================================================

#ifndef __HSLDaisyChainedTiltModule_hsl__
#define __HSLDaisyChainedTiltModule_hsl__	1

#ifndef __HSLStrLib_hsl__
#include "HSLStrLib.hsl"
#endif
#ifndef __HSLMthLib_hsl__
#include "HSLMthLib.hsl"
#endif
#ifndef __HSL_DaisyChainCommunication_hsl__
#include "HSLDaisyChainCommunication.hsl"
#endif


namespace HSLDaisyChainedTiltModule
{
	/*********************************************************************
	*
	* Exported return constants
	*
	*********************************************************************/
	
	const static variable CMD_START   ( 1 );
	const static variable CMD_COMPLET ( 2 );
	const static variable CMD_ERROR   ( 5 );   // to prevent suspicious Phoenix error messages
	const static variable CMD_PROGRESS( 4 );
	const static variable CMD_ERRCOMPL( 5 );

	const variable TRACE_LEVEL_NONE   ( 0 );
	const variable TRACE_LEVEL_NORMAL ( 1 );
   const variable TRACE_LEVEL_FULL   ( 2 );
   const variable HSL_FALSE          ( hslFalse );
   const variable HSL_TRUE           ( hslTrue );
   
	const variable ERROR_NONE                                         (  0 );
   const variable ERROR_COMM_ALREADY_INIT                            (  1 );
	const variable ERROR_PRM_OUT_OF_RANGE                             (  2 );
	const variable ERROR_FALSE_COMM_PORT                              (  3 );
	const variable ERROR_HSL_FUNCTION_FAILED                          (  4 );
	const variable ERROR_WRITE_RECORD_FAILED                          (  5 );
	const variable ERROR_READ_RECORD_FAILED                           (  6 );
	const variable ERROR_UNEXPECTED_TXRX_COMMAND                      (  7 );
	const variable ERROR_UNEXPECTED_TXRX_RESET                        (  8 );
	const variable ERROR_INIT_NOT_FOUND                               (  9 );
	const variable ERROR_STEP_LOST                                    ( 10 );
	const variable ERROR_NOT_INITIALIZED                              ( 11 );
	const variable ERROR_PARALLEL_ERROR                               ( 12 );
	const variable ERROR_STEPPER_MOTOR_HW_DEFECTIVE                   ( 13 );
	const variable ERROR_CMD_UNKNOWN                                  ( 14 );
   const variable ERROR_TEMP_SENSOR_DEFECTIVE                        ( 15 );
   const variable ERROR_TEMP_CONTROLLER_DEFECTIVE                    ( 16 );
   const variable ERROR_UNKNOWN_DEVICE_NAME                          ( 17 );
	const variable ERROR_UNKNOWN_ERROR_CODE                           ( 18 );

	const static string ERROR_MSG_NONE                                ( "No error." );
   const static string ERROR_MSG_COMM_ALREADY_INIT                   ( HSLDaisyChainCommunication::ERROR_MSG_ALREADY_INITIALIZED );
	const static string ERROR_MSG_PRM_OUT_OF_RANGE                    ( HSLDaisyChainCommunication::ERROR_MSG_PARAMETER_OUT_OF_RANGE );
	const static string ERROR_MSG_FALSE_COMM_PORT                     ( HSLDaisyChainCommunication::ERROR_MSG_FALSE_COMM_PORT );
	const static string ERROR_MSG_HSL_FUNCTION_FAILED                 ( HSLDaisyChainCommunication::ERROR_MSG_HSL_FUNCTION_FAILED );
	const static string ERROR_MSG_WRITE_RECORD_FAILED                 ( HSLDaisyChainCommunication::ERROR_MSG_WRITE_RECORD_FAILED );
	const static string ERROR_MSG_READ_RECORD_FAILED                  ( HSLDaisyChainCommunication::ERROR_MSG_READ_RECORD_FAILED );
	const static string ERROR_MSG_UNEXPECTED_TXRX_COMMAND             ( HSLDaisyChainCommunication::ERROR_MSG_UNEXPECTED_TXRX_COMMAND );
	const static string ERROR_MSG_UNEXPECTED_TXRX_RESET               ( HSLDaisyChainCommunication::ERROR_MSG_UNEXPECTED_TXRX_RESET );
	const static string ERROR_MSG_INIT_NOT_FOUND                      ( "Init position not found!" );
	const static string ERROR_MSG_STEP_LOST                           ( "Steps lost (overload)!" );
	const static string ERROR_MSG_NOT_INITIALIZED                     ( "Drive not initialized!" );
	const static string ERROR_MSG_PARALLEL_ERROR                      ( "Parallel functions not allowed!" );
	const static string ERROR_MSG_STEPPER_MOTOR_HW_DEFECTIVE          ( "Stepper motor power amp defective!" );
	const static string ERROR_MSG_CMD_UNKNOWN                         ( "Unknown command!" );
   const static string ERROR_MSG_TEMP_SENSOR_DEFECTIVE               ( "Temperature sensor defective!" );
   const static string ERROR_MSG_TEMP_CONTROLLER_DEFECTIVE           ( "Temperature controoller defective!" );
   const static string ERROR_MSG_UNKNOWN_DEVICE_NAME                 ( "Unknown device name (module identifier)!" );
	const static string ERROR_MSG_UNKNOWN_ERROR_CODE                  ( "Unknown error code!" );



	/*********************************************************************
	*
	* Module globale constants/variables
	*
	*********************************************************************/

	static global variable HSLdaisyChainedTiltModule_SimulationFlag( HSL_FALSE );
	static global variable HSLdaisyChainedTiltModule_TrcLvl( TRACE_LEVEL_NORMAL );


	/*********************************************************************
	*
	* Declarations
	*
	*********************************************************************/

	function Initialize( variable iModuleName, variable iComPort, variable iTraceLevel, variable iSimulate ) variable;
	function Terminate( variable iModuleName ) variable;	
	function MoveToPosition( variable iModuleName, variable iAngle ) variable;
	function StartTempCntr( variable iModuleName, variable iTemperature ) variable;
	function StopTempCntr( variable iModuleName ) variable;
	function RequestTemp( variable iModuleName, variable & oTemperature1, variable & oTemperature2 ) variable;
	function EvaluateErrorMessage( variable iErrorCode ) string;

	private function ValveOn( variable iModuleName, variable iValve ) variable;
	private function ValveOff( variable iModuleName, variable iValve ) variable;
	private function SetPort( variable iModuleName, variable iPort ) variable;
	private function ClearPort( variable iModuleName, variable iPort ) variable;

	static function DecodeTiltModuleErrorCode( variable iErrCode ) variable;
	static function DecodeCommunicationErrorCode( variable iErrCode ) variable;
   static function TxRxCommand( variable iModuleName, variable iCommand, variable iParameter, variable & oResponse ) variable;
   static function TxRxReset( variable iModuleName ) variable;

	
	/*********************************************************************
	*
	* Implementations
	*
	*********************************************************************/

	function Initialize( variable iModuleName, variable iComPort, variable iTraceLevel, variable iSimulate ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;

		
		HSLdaisyChainedTiltModule_SimulationFlag = iSimulate;
		HSLdaisyChainedTiltModule_TrcLvl = iTraceLevel;
		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Initialize system #", iModuleName, " on COM", iComPort );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 1 > iComPort) || ( 255 < iComPort ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter comPort #", iComPort, " out of range [1..255]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// initialize communication port
		// -------------------------------------------------------------------------
      errCode = DecodeCommunicationErrorCode( HSLDaisyChainCommunication::InitializeDaisyChainCommunication( iModuleName, iComPort, iTraceLevel ));
	   if( HSLDaisyChainCommunication::ERROR_NONE != errCode )
   	{
	   	FormatTrace( GetFunctionName(), "()", HSLDaisyChainCommunication::CMD_ERROR, "<InitializeDaisyChainCommunication()> for module ",iModuleName," failed! (Error code = ", errCode, ", error message = ", HSLDaisyChainCommunication::EvaluateCommErrorMessage( errCode ), ")" );
         HSLDaisyChainCommunication::TerminateDaisyChainCommunication( iModuleName );
         return( errCode );
	   }

		// -------------------------------------------------------------------------
		// reset firmware (with configuring timeouts)
		// -------------------------------------------------------------------------
		errCode = TxRxReset( iModuleName );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Resetting firmware system of tilt module failed! (System #", iModuleName, ")" );
			return( errCode );
		}

		// -------------------------------------------------------------------------
		// transmit clear error command
		// -------------------------------------------------------------------------
		command   = "EI";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Resetting errors on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		// -------------------------------------------------------------------------
		// transmit "RF", request firmware version, command and ignore response
		// -------------------------------------------------------------------------
		command   = "RF";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Requesting firmware version of tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}
		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_PROGRESS, "Firmware version of tilt module <", iModuleName, "> is ", response );

		// -------------------------------------------------------------------------
		// transmit "SI", initialize, command
		// -------------------------------------------------------------------------
		command   = "SI";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Initializing of tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}


	
	function Terminate( variable iModuleName ) variable
	{
      variable ret( 0 );
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName );

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "GP0", Go to horizontal position, command
		// -------------------------------------------------------------------------
		command   = "GP";
		parameter = "0";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Moving to zero position on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			ret++;
		}

		// -------------------------------------------------------------------------
		// transmit "PO", Power off, command
		// -------------------------------------------------------------------------
		command   = "PO";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Powering off of tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			ret++;
		}

		// -------------------------------------------------------------------------
		// transmit "TO", Temperature controller off, command
		// -------------------------------------------------------------------------
		command   = "TO";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Switching temperature controller off of tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			ret++;
		}

		// -------------------------------------------------------------------------
		// close comm. port
		// -------------------------------------------------------------------------
      errCode = HSLDaisyChainCommunication::TerminateDaisyChainCommunication( iModuleName );
		if( HSLDaisyChainCommunication::ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " <TerminateDaisyChainCommunication()> to tilt module <", iModuleName, "> failed! (Communication error code = ", errCode, ", error message = ", HSLDaisyChainCommunication::EvaluateCommErrorMessage( errCode ), ")" );
			ret++;
		}

		if( 0 < ret ) return( ERROR_UNKNOWN_ERROR_CODE );

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	function MoveToPosition( variable iModuleName, variable iAngle ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, "to position: angle = ", iAngle );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 0 > iAngle) || ( 10 < iAngle ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter angle = ", iAngle, " out of range [0..10]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "GP", go to position, command
		// -------------------------------------------------------------------------
		command   = "GP";
		parameter = IStr( iAngle );
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Moving tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	function StartTempCntr( variable iModuleName, variable iTemperature ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, "to temperature = ", iTemperature );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 10.0 > iTemperature) || ( 50.0 < iTemperature ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter temperature = ", iTemperature, " out of range [10.0..50.0]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "ST", set temperature, command
		// -------------------------------------------------------------------------
		command   = "ST";
		parameter = IStr( MthFloor( iTemperature * 10 ));
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Starting temperature controller on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	function StopTempCntr( variable iModuleName ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName );

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "TO", temperature controller off, command
		// -------------------------------------------------------------------------
		command   = "TO";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Stopping temperature controller on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	function RequestTemp( variable iModuleName, variable & oTemperature1, variable & oTemperature2 ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName );
      oTemperature1 = 0.0;
      oTemperature2 = 0.0;

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "RT", request temperature, command
		// -------------------------------------------------------------------------
		command   = "RT";
		parameter = "";
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Requesting temperature sensors on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		// -------------------------------------------------------------------------
		// evaluate both temperature sensors values
		// -------------------------------------------------------------------------
		response = StrMid( response, 8, StrGetLength( response ));
		oTemperature1 = StrFVal( StrLeft( response, StrFind( response, " " ))) / 10;
		oTemperature2 = StrFVal( StrRight( response, StrGetLength( response ) - StrFind( response, " " ) - 1 )) / 10;
		
		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Values of temperature sensors: T1 = ", oTemperature1, "°C, T2 = ", oTemperature2, "°C" );
		return( ERROR_NONE );
	}



	function EvaluateErrorMessage( variable iErrorCode ) string
	{
   	     if( iErrorCode == ERROR_NONE                                    ) return( ERROR_MSG_NONE );
      else if( iErrorCode == ERROR_COMM_ALREADY_INIT                       ) return( ERROR_MSG_COMM_ALREADY_INIT );
   	else if( iErrorCode == ERROR_PRM_OUT_OF_RANGE                        ) return( ERROR_MSG_PRM_OUT_OF_RANGE );
   	else if( iErrorCode == ERROR_FALSE_COMM_PORT                         ) return( ERROR_MSG_FALSE_COMM_PORT );
   	else if( iErrorCode == ERROR_HSL_FUNCTION_FAILED                     ) return( ERROR_MSG_HSL_FUNCTION_FAILED );
   	else if( iErrorCode == ERROR_WRITE_RECORD_FAILED                     ) return( ERROR_MSG_WRITE_RECORD_FAILED );
   	else if( iErrorCode == ERROR_READ_RECORD_FAILED                      ) return( ERROR_MSG_READ_RECORD_FAILED );
   	else if( iErrorCode == ERROR_UNEXPECTED_TXRX_COMMAND                 ) return( ERROR_MSG_UNEXPECTED_TXRX_COMMAND );
   	else if( iErrorCode == ERROR_UNEXPECTED_TXRX_RESET                   ) return( ERROR_MSG_UNEXPECTED_TXRX_RESET );
   	else if( iErrorCode == ERROR_INIT_NOT_FOUND                          ) return( ERROR_MSG_INIT_NOT_FOUND );
   	else if( iErrorCode == ERROR_STEP_LOST                               ) return( ERROR_MSG_STEP_LOST );
   	else if( iErrorCode == ERROR_NOT_INITIALIZED                         ) return( ERROR_MSG_NOT_INITIALIZED );
   	else if( iErrorCode == ERROR_PARALLEL_ERROR                          ) return( ERROR_MSG_PARALLEL_ERROR );
   	else if( iErrorCode == ERROR_STEPPER_MOTOR_HW_DEFECTIVE              ) return( ERROR_MSG_STEPPER_MOTOR_HW_DEFECTIVE );
   	else if( iErrorCode == ERROR_CMD_UNKNOWN                             ) return( ERROR_MSG_CMD_UNKNOWN );
      else if( iErrorCode == ERROR_TEMP_SENSOR_DEFECTIVE                   ) return( ERROR_MSG_TEMP_SENSOR_DEFECTIVE );
      else if( iErrorCode == ERROR_TEMP_CONTROLLER_DEFECTIVE               ) return( ERROR_MSG_TEMP_CONTROLLER_DEFECTIVE );
      else if( iErrorCode == ERROR_UNKNOWN_DEVICE_NAME                     ) return( ERROR_MSG_UNKNOWN_DEVICE_NAME );
   	else if( iErrorCode == ERROR_UNKNOWN_ERROR_CODE                      ) return( ERROR_MSG_UNKNOWN_ERROR_CODE );
   	else                                                                   return( ERROR_MSG_UNKNOWN_ERROR_CODE );
	}




	private function ValveOn( variable iModuleName, variable iValve ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, " valve #", iValve );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 1 > iValve) || ( 6 < iValve ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter valve = ", iValve, " out of range [1..6]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}
		iValve = iValve + 2;

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "PS", port set, command
		// -------------------------------------------------------------------------
		command   = "PS";
		parameter = IStr( iValve );
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Switching valve \"on\" on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	private function ValveOff( variable iModuleName, variable iValve ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, " valve #", iValve );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 1 > iValve) || ( 6 < iValve ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter valve = ", iValve, " out of range [1..6]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}
		iValve = iValve + 2;

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "PC", port clear, command
		// -------------------------------------------------------------------------
		command   = "PC";
		parameter = IStr( iValve );
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Switching valve \"off\" on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	private function SetPort( variable iModuleName, variable iPort ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, " port #", iPort );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 1 > iPort) || ( 8 < iPort ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter port #", iPort, " out of range [1.8]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "PS", port set, command
		// -------------------------------------------------------------------------
		command   = "PS";
		parameter = IStr( iPort );
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Setting port on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	private function ClearPort( variable iModuleName, variable iPort ) variable
	{
		variable command;
		variable parameter;
		variable response;
		variable errCode;


		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace(GetFunctionName(), "()", CMD_START, " Module #", iModuleName, " port #", iPort );

		// -------------------------------------------------------------------------
		// check parameters
		// -------------------------------------------------------------------------
		if(( 1 > iPort) || ( 8 < iPort ))
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Parameter port #", iPort, " out of range [1.8]!" );
			return( ERROR_PRM_OUT_OF_RANGE );
		}

		// -------------------------------------------------------------------------
		// check simulation mode
		// -------------------------------------------------------------------------
		if(( GetSimulationMode() == 1 ) || ( hslTrue == HSLdaisyChainedTiltModule_SimulationFlag ))
		{
			if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET, " Tilt module runs in simulation mode!" );
			return( ERROR_NONE );
		}

		// -------------------------------------------------------------------------
		// transmit "PC", port clear, command
		// -------------------------------------------------------------------------
		command   = "PC";
		parameter = IStr( iPort );
		response  = "";
		errCode = TxRxCommand( iModuleName, command, parameter, response );
		if( ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Clearing port on tilt module failed! (System #", iModuleName, ", TxRxCommand(", command, parameter, "), response = <", response, ">)" );
			return( errCode );
		}

		if( TRACE_LEVEL_NONE < HSLdaisyChainedTiltModule_TrcLvl ) FormatTrace( GetFunctionName(), "()", CMD_COMPLET );
		return( ERROR_NONE );
	}



	static function DecodeTiltModuleErrorCode( variable iErrCode ) variable
	{
		     if(  0 == iErrCode ) return( ERROR_NONE );
		else if(  1 == iErrCode ) return( ERROR_INIT_NOT_FOUND );
		else if(  2 == iErrCode ) return( ERROR_STEP_LOST );
		else if(  3 == iErrCode ) return( ERROR_NOT_INITIALIZED );
		else if(  4 == iErrCode ) return( ERROR_PARALLEL_ERROR );
		else if(  5 == iErrCode ) return( ERROR_STEPPER_MOTOR_HW_DEFECTIVE );
		else if(  6 == iErrCode ) return( ERROR_PRM_OUT_OF_RANGE );
		else if(  7 == iErrCode ) return( ERROR_CMD_UNKNOWN );
		else if(  8 == iErrCode ) return( ERROR_TEMP_SENSOR_DEFECTIVE );
		else if(  9 == iErrCode ) return( ERROR_TEMP_CONTROLLER_DEFECTIVE );
   	else                      return( ERROR_UNKNOWN_ERROR_CODE );
	}



	static function DecodeCommunicationErrorCode( variable iErrCode ) variable
	{
		     if( HSLDaisyChainCommunication::ERROR_NONE                    == iErrCode ) return( ERROR_NONE );
		else if( HSLDaisyChainCommunication::ERROR_ALREADY_INITIALIZED     == iErrCode ) return( ERROR_COMM_ALREADY_INIT );
		else if( HSLDaisyChainCommunication::ERROR_PARAMETER_OUT_OF_RANGE  == iErrCode ) return( ERROR_PRM_OUT_OF_RANGE );
		else if( HSLDaisyChainCommunication::ERROR_FALSE_COMM_PORT         == iErrCode ) return( ERROR_FALSE_COMM_PORT );
		else if( HSLDaisyChainCommunication::ERROR_HSL_FUNCTION_FAILED     == iErrCode ) return( ERROR_HSL_FUNCTION_FAILED );
		else if( HSLDaisyChainCommunication::ERROR_WRITE_RECORD_FAILED     == iErrCode ) return( ERROR_WRITE_RECORD_FAILED );
		else if( HSLDaisyChainCommunication::ERROR_READ_RECORD_FAILED      == iErrCode ) return( ERROR_READ_RECORD_FAILED );
		else if( HSLDaisyChainCommunication::ERROR_UNEXPECTED_TXRX_COMMAND == iErrCode ) return( ERROR_UNEXPECTED_TXRX_COMMAND );
		else if( HSLDaisyChainCommunication::ERROR_UNEXPECTED_TXRX_RESET   == iErrCode ) return( ERROR_UNEXPECTED_TXRX_RESET );
   	else                                                                             return( ERROR_UNKNOWN_ERROR_CODE );
	}



   static function TxRxCommand( variable iModuleName, variable iCommand, variable iParameter, variable & oResponse ) variable
   {
      variable errCode;

      errCode = HSLDaisyChainCommunication::DaisyChainedTxRxCommand( iModuleName, iCommand, iParameter, oResponse );
		if( HSLDaisyChainCommunication::ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Communication error occurred! (System #", iModuleName, ", Error Text: ", HSLDaisyChainCommunication::EvaluateCommErrorMessage( errCode ), ")" );
			return( DecodeCommunicationErrorCode( errCode ));
		}
      if( StrGetLength( oResponse ) < 8 )
      {
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Length of response too short! (System #", iModuleName, ", Error Text: ", EvaluateErrorMessage( ERROR_UNKNOWN_DEVICE_NAME ), ")" );
         return( ERROR_UNKNOWN_DEVICE_NAME );
      }
		errCode = DecodeTiltModuleErrorCode( IVal( StrMid( oResponse, 6, 2 )));
      if( errCode != ERROR_NONE )
      {
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Tilt module error occurred! (System #", iModuleName, ", Error Text: ", EvaluateErrorMessage( errCode ), ")" );
      }
		return( errCode );
   }



   static function TxRxReset( variable iModuleName ) variable
   {
      variable errCode;
      variable response;

      errCode = HSLDaisyChainCommunication::DaisyChainedTxRxReset( iModuleName, response );
		if( HSLDaisyChainCommunication::ERROR_NONE != errCode )
		{
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Communication error occurred! (System #", iModuleName, ", Error Text: ", HSLDaisyChainCommunication::EvaluateCommErrorMessage( errCode ), ")" );
			return( DecodeCommunicationErrorCode( errCode ));
		}
      if( StrGetLength( response ) < 8 )
      {
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Length of response too short! (System #", iModuleName, ", Error Text: ", EvaluateErrorMessage( ERROR_UNKNOWN_DEVICE_NAME ), ")" );
         return( ERROR_UNKNOWN_DEVICE_NAME );
      }
		errCode = DecodeTiltModuleErrorCode( IVal( StrMid( response, 6, 2 )));
      if( errCode != ERROR_NONE )
      {
			FormatTrace( GetFunctionName(), "()", CMD_ERROR, " Tilt module error occurred! (System #", iModuleName, ", Error Text: ", EvaluateErrorMessage( errCode ), ")" );
      }
		return( errCode );
   }




}	//end namespace 
#endif
// $$author=rvogel$$valid=0$$time=2008-07-08 10:01$$checksum=e793eb0b$$length=085$$